plugins {
	id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

// Dynamic version mapping based on Minecraft version
class ModVersions {
	String fabricApi
	String clothConfig
	String minecraftVersion
	String minClothConfig
	String modMenu
	
	ModVersions(String fabricApi, String clothConfig, String minecraftVersion, String minClothConfig, String modMenu) {
		this.fabricApi = fabricApi
		this.clothConfig = clothConfig
		this.minecraftVersion = minecraftVersion
		this.minClothConfig = minClothConfig
		this.modMenu = modMenu
	}
}

def versionMap = [
	"1.21": new ModVersions("0.100.8+1.21", "15.0.140", ">=1.21 <1.21.2", ">=15.0.0", "11.0.3"),
	"1.21.1": new ModVersions("0.104.0+1.21.1", "15.0.140", ">=1.21 <1.21.3", ">=15.0.0", "11.0.3"),
	"1.21.2": new ModVersions("0.107.0+1.21.3", "16.0.141", ">=1.21.2 <1.21.4", ">=16.0.0", "12.0.0"),
	"1.21.3": new ModVersions("0.107.0+1.21.3", "16.0.141", ">=1.21.2 <1.21.5", ">=16.0.0", "12.0.0"),
	"1.21.4": new ModVersions("0.113.0+1.21.4", "17.0.144", ">=1.21.4 <1.21.6", ">=17.0.0", "13.0.0"),
	"1.21.5": new ModVersions("0.126.0+1.21.5", "18.0.145", ">=1.21.5 <1.21.7", ">=18.0.0", "14.0.0"),
	"1.21.6": new ModVersions("0.127.1+1.21.6", "19.0.147", ">=1.21.6 <1.21.9", ">=19.0.0", "15.0.0-beta.3"),
	"1.21.7": new ModVersions("0.127.1+1.21.6", "19.0.147", ">=1.21.6 <1.21.9", ">=19.0.0", "15.0.0-beta.3"),
	"1.21.8": new ModVersions("0.127.1+1.21.6", "19.0.147", ">=1.21.6 <1.21.10", ">=19.0.0", "15.0.0-beta.3"),
	"1.21.11": new ModVersions("0.141.1+1.21.11", "21.11.153", ">=1.21.11 <1.22", ">=21.11.0", "15.0.0-beta.3")
]

def mcVersion = project.minecraft_version
def modVersions = versionMap.get(mcVersion)

if (modVersions == null) {
	throw new GradleException("No version mapping found for Minecraft ${mcVersion}. Please add it to the version map in build.gradle")
}

println "Building for Minecraft ${mcVersion}"
println "Using Fabric API: ${modVersions.fabricApi}"
println "Using Cloth Config: ${modVersions.clothConfig}"
println "Using Mod Menu: ${modVersions.modMenu}"

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
}

loom {
	runs {
		client {
			// Set custom player name for development
			programArgs "--username", "DevPlayer_01"
			// Optional: Set UUID (if needed)
			// programArgs "--uuid", "12345678-1234-1234-1234-123456789012"
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API - dynamically set based on Minecraft version
	modImplementation "net.fabricmc.fabric-api:fabric-api:${modVersions.fabricApi}"
	
	// Cloth Config - dynamically set based on Minecraft version
	modImplementation "me.shedaniel.cloth:cloth-config-fabric:${modVersions.clothConfig}"
	
	// Mod Menu integration - dynamically set based on Minecraft version
	modImplementation "com.terraformersmc:modmenu:${modVersions.modMenu}"
}

processResources {
	inputs.property "version", project.version
	inputs.property "minecraft_version", modVersions.minecraftVersion
	inputs.property "min_cloth_config", modVersions.minClothConfig

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version,
		       "minecraft_version": inputs.properties.minecraft_version,
		       "min_cloth_config": inputs.properties.min_cloth_config
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}